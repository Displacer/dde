<?xml version="1.0" encoding="windows-1251"?>
<component>

<?component error="true" debug="true"?>

<registration
	description="quik"
	progid="quik.WSC"
	version="1.00"
	classid="{a16bd638-62bc-4fc5-ad08-a2b81c41085f}"
>
</registration>

<public>
	<method name="StartEXE">
		<PARAMETER name="path"/>
		<PARAMETER name="cmdline"/>
		<PARAMETER name="params"/>
	</method>
	<method name="ErrorEXE">
		<PARAMETER name="Err_Description"/>
	</method>	
	<method name="StopEXE">
	</method>	
	
</public>

<script language="VBScript">
<![CDATA[
Public CMDPARMS

Dim HTA,LOG,YAKOR
Dim IsLOG
Dim SN

Function StartEXE(path, cmdLine, params)
'Здесь определяются настройки программы DDE.EXE 
'кратко это обычный парсер строки параметров dde.exe
'это пример, вы можете написать свой алгоритм, например считывать ini файл итд.
'path    - путь где находится DDE.EXE
'cmdline - текстовый параметр DDE.EXE /T4A=TVS....., см START.VBS
'          тогда cmdline="/T4A=TVS....."
'          или   cmdline="SN=ddeTVS;T=[TVS]AKCII;COM=script:C:\TEST\analys.wsc;..."
'params  - объект DICTIONARY, который должен быть заполнен след. обязат-ми. значениями
'params("servername")   имя DDE сервера
'params("tbl4analysis") [ИМЯ_ТАБЛИЦЫ]ЛИСТ данные из окна настройки ДДЕ квика 
'params("obj4analysis") см ниже
'params("pipe_app")     см ниже
'params("dic")          объект DICTIONARY который будет передан в obj4analysis.wsc
'params("rows_num")     кол-во строк которое дде передаст в пайп

'вдруг запустили без параметров тогда сделаем по умолчанию
'см также БыстрыйСтарт1.vbs там запуск dde.exe без параметров
params("servername")="excel"
params("rows_num")=0
params("obj4analysis")=""
params("pipe_app")="self"

IsLOG=True
On Error Resume Next
Set LOG = CreateObject("FindHTA68.HTA68").HTA("LOG") 
Set YAKOR=LOG.frames("GRID").document.all("YAKOR") 
If Err.Number<>0 Then IsLOG=False
On Error GoTo 0

WriteLOG "Стартует DDE.WSC (" & cmdLine & ")"

'поделим cmdline в массив cmd()
CMD=Split(cmdLine,";")  
For i=0 To UBound(CMD)  'переберем весь массив
    P=Split(CMD(i),"=") 'Например CMD(0)="SN" и CMD(1)="ddeTVS"
    P(0)=Trim(P(0)):P(1)=Trim(P(1))
    'Заполним params
    Select Case P(0)
           Case "SN" 
                 params("servername")=P(1)         'имя DDE сервера
           Case "T4A" 
                 params("tbl4analysis")=P(1)       'из какой таблицы КВИКА данные будут обрабработаны в анализе, см ниже
           Case "COM"
                'Параметр obj4analysis может быть 3 видов
                'params("obj4analysis")="SCRIPT:C:\TEST\analys.wsc"     'скриптовый ком
                'params("obj4analysis")="MYCOM.COM"                     'ваш ком на C++/DELPHI/VB/WSC(рег)
                'params("obj4analysis")=""                              'Не запускать процесс анализа, тогда "T4A" может быть пустой   
                params("obj4analysis")=P(1)
           Case "P"
                'DDE сервер будет писать данные из таблиц квика  построчно
                'Пример 
                '[TIP]GAZ=Вермя;Инструмент;Дата торгов;Время изм.;Цена посл.;…
                '[TIP]GAZ=22:57:24;GZH9;11.03.2009;22:57:23;12414;…
                'params("pipe_app")="SELF", то поступающие данные из таблиц квика будут писаться в родной STDOUT DDE.EXE,
                'если пусто то вывод таблиц не идет
              	params("pipe_app")=P(1)
           Case "RN"  'обязательный параметр   
                '0 все строки из таблицы
                'N- n строк. Удобно для стаканов, брать только лучшие цены
                params("rows_num")=Clng(P(1))
    End Select       
Next

'params("dic")-это объект DICTIONARY параметров/настроек для анализа в ком (см Case "COM") и analys.wsc
Set analiz_parm=CreateObject("Scripting.Dictionary")
analiz_parm("WHERE_WE")=path
analiz_parm("SN")=params("servername")
analiz_parm("T")=params("tbl4analysis")

params.Add "dic",analiz_parm
Set analiz_parm=Nothing
Set LOG   = Nothing
Set YAKOR = Nothing

CMDPARMS=cmdline

'-1-незамедлительный выход с сообщением об ошибке
' 0-незамедлительный выход без сообщения
' 1-продолжение работы
StartEXE=1 
End function

Function ErrorEXE(Err_Description)
         'В случае если произошла ошибка в DDE.EXE  
	 MsgBox "[" & CMDPARMS & "]"  & vbCrLf & Err_Description,0
End Function

Function StopEXE()
         MsgBox "[" & CMDPARMS & "]"  & vbCrLf & "Конец работы командой exit.exe",0
End Function


'Запись в лог окно
Function WriteLOG(what)
On Error Resume Next
YAKOR.insertAdjacentText "BeforeEnd","[dde.wsc]" & what & vbCrLf 
YAKOR.Document.Body.DoScroll
End Function

]]>
</script>

</component>
